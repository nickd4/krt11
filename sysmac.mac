.MACRO	..CM.
;
;                   COPYRIGHT (c) 1986 BY
;             DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;			  ALL RIGHTS RESERVED
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS
; SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.
.ENDM
.MACRO	..V1..
.MCALL	...CM0,...CM1,...CM2,...CM3,...CM4,...CM5,...CM6,...CM7
...V1=1
.ENDM
.MACRO	..V2..
.MCALL	...CM0,...CM1,...CM2,...CM3,...CM4,...CM5,...CM6,...CM7
...V1=2.
.ENDM
.MACRO	.MACS
.MCALL	...CM0,...CM1,...CM2,...CM3,...CM4,...CM5,...CM6,...CM7
...V1=3.
.ENDM
.MACRO	...CM0 STARG,INS
.IF B ^/STARG/
	CLR	-(SP)
.IFF
.IF IDN ^/STARG/,#0
	CLR	-(SP)
.IFF
.IIF IDN ^/STARG/ ^/0/ .ERROR;?SYSMAC-W-Invalid argument, use #0, not 0;
	MOV	STARG,-(SP)
.ENDC
.ENDC
.IF NB ^/INS/
	EMT	^/INS/
.ENDC
.ENDM
.MACRO	...CM1	AREA,IC,CHAN,FLAG,ARG,INS,CSET,BB
...CM5	^/AREA/
...V2=0
.IF B ^/FLAG/
.IIF B ^/AREA/,...V2=1
.IFF
.IIF DIF ^/FLAG/,SET,...V2=1
.ENDC
.IF NE ...V2
.IF IDN ^/CHAN/,^/#0/
	CLRB	@R0
.IFF
.IF NB ^/CHAN/
	MOVB	CHAN,@R0
.ENDC
.ENDC
.IFF
.IF B ^/CHAN/
	MOVB	#IC'.,1(R0)
.IFF
.NTYPE ...V2,CHAN
.IF EQ ...V2-0O27
	MOV	CHAN+^/IC'.*0O400/,@R0
.IFF
	MOV	#IC'.*0O400,@R0
	MOVB	CHAN,@R0
.ENDC
.ENDC
.ENDC
.IIF IDN ^/CHAN/ ^/0/ .ERROR;?SYSMAC-W-Invalid argument, use #0, not 0;
...CM2	^/ARG/,2,INS,CSET,BB
.ENDM
.MACRO	...CM2	ARG,OFFSE,INS,CSET,BB
.IF B ^/ARG/
.IF NB ^/CSET/
.IF NE ...V1-3.
	CLR'BB	OFFSE'.(R0)
.ENDC
.ENDC
.IFF
.IF IDN	^/ARG/,#0
	CLR'BB	OFFSE'.(R0)
.IFF
.IIF IDN ^/ARG/ ^/0/ .ERROR;?SYSMAC-W-Invalid argument, use #0, not 0;
	MOV'BB	ARG,OFFSE'.(R0)
.ENDC
.ENDC
.IF NB ^/INS/
	EMT	0O375
.ENDC
.ENDM
.MACRO	...CM3	CHAN,IC
.IF B ^/CHAN/
	MOV	#IC*0O400,R0
.IFF
.NTYPE ...V2,CHAN
.IF EQ ...V2-0O27
	MOV	CHAN+^/IC*0O400/,R0
.IFF
	MOV	#IC*0O400,R0
	BISB	CHAN,R0
.ENDC
.ENDC
	EMT	0O374
.ENDM
.MACRO	...CM4	AREA,BUF,WCNT,CRTN,IC,CODE
.IIF IDN ^/CODE/,NOSET ...CM1 ^/AREA/,IC,,^/CODE/
.IIF DIF ^/CODE/,NOSET ...CM1 ^/AREA/,IC,#0,^/CODE/
...CM2 ^/BUF/,4
...CM2 ^/WCNT/,6
...CM2 ^/CRTN/,8,E
.ENDM
.MACRO	...CM5	SRC,INS,BB
.IF NB ^/SRC/
.IF DIF ^/SRC/,R0
	MOV'BB	SRC,R0
.ENDC
.ENDC
.IF NB ^/INS/
	EMT	^/INS/
.ENDC
.ENDM
.MACRO	...CM6	AREA,IC,CHAN,FLAG,ARG,INS,CSET,BB
...CM5	^/AREA/
.IF B ^/FLAG/
.IF NB ^/AREA/
	MOV	#IC'.*0O400+CHAN'.,@R0
.ENDC
.IFF
.IF IDN ^/FLAG/,SET
	MOV	#IC'.*0O400+CHAN'.,@R0
.ENDC
.ENDC
...CM2	^/ARG/,2,INS,CSET,BB
.ENDM
.MACRO	...CM7	AREA,CHAN,BUF,WCNT,BLK,CRTN,IC,CODE,V1
.IF EQ ...V1-1
...CM5	^/WCNT/
...CM0	^/CRTN/
...CM0	^/BUF/
...CM0	^/CHAN/,^/V1+AREA/
.IFF
...CM1 ^/AREA/,IC,^/CHAN/,^/CODE/,^/BLK/
...CM2 ^/BUF/,4
...CM2 ^/WCNT/,6
...CM2 ^/CRTN/,8,E
.ENDC
.ENDM
.MACRO	...CMV	TYPE=Z,PART=ALL,XPREFIX=V,XRELEASE=05,XVERSION=03,XSUFFIX=^/ /
.IF	IDN	^/PART/ ^/ALL/
.IF	IDN	^/TYPE/ ^/V/
RT$PRE	=:	''XPREFIX
RT$REL	=:	'XRELEASE'.
RT$VER	=:	'XVERSION'.
RT$SUF	=:	''XSUFFIX
.IFF
	.ASCI'TYPE'	"'XPREFIX''XRELEASE'.'XVERSION' 'XSUFFIX'"
.ENDC
.ENDC
.IF	IDN	^/PART/ ^/PREFIX/
.IF	IDN	^/TYPE/ ^/V/
RT$PRE	=:	''XPREFIX
.IFF
	.ASCI'TYPE'	"'XPREFIX'"
.ENDC
.ENDC
.IF	IDN	^/PART/ ^/RELEASE/
.IF	IDN	^/TYPE/ ^/V/
RT$REL	=:	'XRELEASE'.
.IFF
	.ASCI'TYPE'	"'XRELEASE'"
.ENDC
.ENDC
.IF	IDN	^/PART/ ^/VERSION/
.IF	IDN	^/TYPE/ ^/V/
RT$VER	=:	'XVERSION'.
.IFF
	.ASCI'TYPE'	"'XVERSION'"
.ENDC
.ENDC
.IF	IDN	^/PART/ ^/SUFFIX/
.IF	IDN	^/TYPE/ ^/V/
RT$SUF	=:	''XSUFFIX
.IFF
	.ASCI'TYPE'	"'XSUFFIX'"
.ENDC
.ENDC
.ENDM
.MACRO	...CMY	L0,L1
.ENDM
.MACRO	...CMZ	FUDGE
.ENDM
.MACRO	.ABTIO	CHAN
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM3	^/CHAN/,11.
.ENDM
.MACRO	.ADDR	ADR,REG,TYPE
.NTYPE	...V2,ADR
.IIF NE	...V2-0o27 .ERROR ...V2; ?SYSMAC-F-Invalid A D R, expecting #..., found - ADR ;
.NTYPE	...V2,REG
.IF EQ	...V2-0o46
.IF  IDN ^/TYPE/,^/ADD/
	ADD	PC,-(SP)
.ERROR ; ?SYSMAC-W-This operation may produce random results ;
.IFF
	MOV	PC,-(SP)
.ENDC
	ADD	ADR-.,@SP
.MEXIT
.ENDC
.IF IDN ^/TYPE/,^/PUSH/
.IF GT	...V2-5.
	MOV	REG,-(SP)
.IFF
	JSR	REG,@PC
	ADD	ADR-.,REG
.MEXIT
.ENDC
.ENDC
.IF IDN	^/TYPE/,^/ADD/
	ADD	PC,REG
.IFF
	MOV	PC,REG
.ENDC
	ADD	ADR-.,REG
.IRP	x,0,1,2,3,4,5,10,11,12,13,14,15,16
.IIF EQ	...V2-0o'x ...V2=0
.ENDR
.IIF NE ...V2 .ERROR ; ?SYSMAC-F-Invalid R E G expecting - Rx, @Rx, or -(SP) - found REG ;
.ENDM
.MACRO	.ASSUME	A,REL,C,MESSAGE
.IF REL	^?^/A/?-^?^/C/?
.IFF
.IF B ^/MESSAGE/
.ERROR;"A REL C" is not true;
.IFF
.ERROR	MESSAGE
.ENDC
.ENDC
.ENDM	.ASSUME
.MACRO	.AUDIT	Q,W,E,R,T,Y,U,I,O,P,A,S,D,F,G,H,J,K,L,Z,X,C,V,B,N,M
.SAVE
.ASECT
.IIF	NDF	...V5	...V5=0O110
.IF	DF,...V6
.IF	EQ,...V6-2
...V5=0O110
...V6 = 0
.ENDC
.ENDC
.=...V5
.IF	EQ	.-0O110
	.GLOBL	.AUDIT
	.WORD	.AUDIT
.ENDC
.IRP	...V2 Q,W,E,R,T,Y,U,I,O,P,A,S,D,F,G,H,J,K,L,Z,X,C,V,B,N,M
.IF	NB	^/...V2/
	.GLOBL	'...V2'
	.WORD	'...V2'
.ENDC
.ENDR
...V5=.
	.WORD	-1
.RESTORE
.ENDM	.AUDIT
; remove due to conflict, as it was not imported by .mcall
;.MACRO	.BR	TO
;.IF DF	TO
;.IF NE	TO-.
;.ERROR;NOT AT LOCATION "TO";
;.ENDC
;.ENDC
;.ENDM	.BR
.MACRO	.CDFN	AREA,ADDR,NUM,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,13,0,^/CODE/,^/ADDR/
...CM2	^/NUM/,4,E
.ENDM
.MACRO	.CHAIN
	MOV	#0O4000,R0
	EMT	0O374
.ENDM
.MACRO	.CHCOP	AREA,CHAN,OCHAN,JOBBLK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM1	^/AREA/,11,^/CHAN/,^/CODE/,^/OCHAN/
.IF NB ^/JOBBLK/
...CM2	^/JOBBLK/,4,E
.IFF
...CM2	#0,4,E
.ENDC
.ENDM
.MACRO	.CKXX	REG
.IRP	X,REG
.MACRO	CK.'X	LABEL,CHANGE,RESULT
.IF NB ^/CHANGE/
.IIF LT CHANGE,CK.'X=CK.'X''CHANGE
.IFTF
.IIF NB ^/LABEL/,				.Assume CK.'X EQ LABEL
.IFT
.IIF GT CHANGE,CK.'X=CK.'X''CHANGE
.ENDC
.IIF NB ^/RESULT/	RESULT=CK.'X
.ENDM
CK.'X=152525
.ENDR
.ENDM
.MACRO	.CLOSE	CHAN
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
	EMT	^/160+CHAN/
.IFF
...CM3 ^/CHAN/,6.
.ENDC
.ENDM
.MACRO	.CMKT	AREA,ID,TIME=#0,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,19,0,^/CODE/,^/ID/
...CM2	^/TIME/,4,E,C
.ENDM
.MACRO	.CNTXS	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,27,0,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.CRAW	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,30,2,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.CRRG	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,30,0,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.CSIGE	DEVSPC,DEFEXT,CSTRNG,LINBUF
.IIF IDN ^/DEVSPC/ ^/#0/ .ERROR;?SYSMAC-E-Invalid argument, #0 in first argument position ;
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF NB ^/LINBUF/
...CM0	^/LINBUF/
.NTYPE	...V2,DEVSPC
.IF EQ ...V2-0O27
...CM0	^/DEVSPC'+1/
.IFF
...CM0	^/DEVSPC/
	INC	(SP)
.ENDC
.IFF
...CM0	^/DEVSPC/
.ENDC
...CM0	^/DEFEXT/
...CM0	^/CSTRNG/,344
.ENDM
.MACRO	.CSISPC	OUTSPC,DEFEXT,CSTRNG,LINBUF
.IIF IDN ^/OUTSPC/ ^/#0/ .ERROR;?SYSMAC-E-Invalid argument, #0 in first argument position ;
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF NB ^/LINBUF/
...CM0	^/LINBUF/
.NTYPE	...V2,OUTSPC
.IF EQ ...V2-0O27
...CM0	^/OUTSPC'+1/
.IFF
...CM0	^/OUTSPC/
	INC	(SP)
.ENDC
.IFF
...CM0	^/OUTSPC/
.ENDC
...CM0	^/DEFEXT/
...CM0	^/CSTRNG/,345
.ENDM
.MACRO	.CSTAT	AREA,CHAN,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM1	^/AREA/,23,^/CHAN/,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.CTIMI	TBK
	JSR	R5,@$TIMIT
	.WORD	TBK-.
	.WORD	1
.ENDM
.MACRO	.DATE
	MOV	#0O5000,R0
	EMT	0O374
.ENDM
.MACRO	.DELETE	AREA,CHAN,DBLK,SEQNUM,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
...CM5	^/CHAN/,^/AREA/
.IFF
...CM5	^/AREA/
.IF IDN ^/CHAN/,#0
	CLR	@R0
.IFF
.IIF IDN ^/CHAN/ ^/0/ .ERROR;?SYSMAC-W-Invalid argument, use #0, not 0;
...V2=0
.IF B ^/CODE/
.IIF B ^/AREA/,...V2=1
.IFF
.IIF DIF ^/CODE/,SET,...V2=1
.ENDC
.IF NE ...V2
.IF NB ^/CHAN/
	MOVB	CHAN,@R0
.ENDC
.IFF
.IF B ^/CHAN/
	CLRB	1(R0)
.IFF
.NTYPE ...V2,CHAN
.IF EQ ...V2-0O27
	MOV	CHAN,@R0
.IFF
	CLR	@R0
	MOVB	CHAN,@R0
.ENDC
.ENDC
.ENDC
.ENDC
...CM2	^/DBLK/,2
...CM2	^/SEQNUM/,4,E,C
.ENDC
.ENDM
.MACRO	.DEVIC	AREA,ADDR,LINK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF B LINK
...CM6	^/AREA/,12,0,^/CODE/,^/ADDR/,E
.IFF
...CM6	^/AREA/,12,1,^/CODE/,^/ADDR/,E
.ENDC
.ENDM
.MACRO	.DRAST	NAME,PRI,ABT
.GLOBL	$INPTR
.IF B ^/ABT/
	RETURN
.IFF
	BR	ABT
.ENDC
NAME'INT::JSR	R5,@$INPTR
	.WORD	^C^/PRI*0O40/&^O340
.ENDM
.MACRO	.DRBEG	NAME,VEC,DSIZ,DSTS,VTBL
.IIF NDF ...V9 ...V9=0
.IIF NDF ...V19 ...V19=0
.ASECT
.=0O52
.GLOBL	NAME'END,NAME'INT
	.WORD	^/NAME'END-NAME'STRT/
.IF B	^/DSIZ/
	.WORD	NAME'DSIZE
.IFF
	.WORD	DSIZ
.ENDC
.IF B	^/DSTS/
	.WORD	NAME'STS
.IFF
	.WORD	DSTS
.ENDC
	.WORD	^?ERL$G+^/MMG$T*2/+^/TIM$IT*4/+^/RTE$M*10/?
.PSECT	NAME'DVR
NAME'STRT::
.IF NB	VTBL
.GLOBL	VTBL
	.WORD	^/VTBL-.//2.-1+0O100000
.IFF
.IF NB	^/VEC/
.IIF NE	VEC&3.,.ERROR	VEC;?SYSMAC-E-Odd or invalid vector specified;
	.WORD	VEC&^C3.
.IFF
.IF DF	NAME'$VTB
.GLOBL	NAME'$VTB
	.WORD	^/NAME'$VTB-.//2.-1+0O100000
.IFF
.IIF NE	NAME'$VEC&3.,.ERROR	NAME'$VEC;SYSMAC-E-Odd or invalid vector specified;
	.WORD	NAME'$VEC&^C3.
.ENDC
.ENDC
.ENDC
	.WORD	NAME'INT-.,0O340
NAME'SYS::
NAME'LQE::	.WORD	0
.IRP	x,\...V19
NAME'CQE::	.WORD	x
.ENDR
.IRP	x,/0o240!...V9
		.WORD	x
.ENDR
.ENDM
.MACRO	.DRBOT	NAME,ENTRY,READ,CONTROL=^/UBUS,QBUS/,SIDES=1,OFFSET=NO
...V7=-1
.IIF IDN OFFSET,YES,...V7=0
	.DREND	NAME
.IIF NDF TPS,TPS=:0O177564
.IIF NDF TPB,TPB=:0O177566
LF=:0O12
CR=:0O15
B$BOOT=:0O1000
B$DEVN=:0O4716
B$DEVU=:0O4722
B$READ=:0O4730
.IF EQ	MMG$T
B$DNAM=:^R'NAME
.IFF
B$DNAM=:^R'NAME'X
.ENDC
.ASECT
.=0O62
	.WORD	NAME'BOOT,NAME'BEND-NAME'BOOT,READ-NAME'BOOT
.PSECT	NAME'BOOT
NAME'BOOT::NOP
	BR	ENTRY-2.
...V2=0O100
.IRP	X	CONTROL
...V3=0
.IIF	IDN	^/X/	^/UBUS/	...V3=1.
.IIF	IDN	^/X/	^/QBUS/	...V3=2.
.IIF	IDN	^/X/	^/CBUS/	...V3=4.
.IIF	IDN	^/X/	^/UMSCP/	...V3=0O10
.IIF	IDN	^/X/	^/QMSCP/	...V3=0O20
.IIF	IDN	^/X/	^/CMSCP/	...V3=0O40
.IIF	EQ	...V3	.ERROR;?SYSMAC-E-Invalid c o n t r o l value - control;
...V2=...V2!...V3
.ENDR
.=ENTRY-6.
	.BYTE	0O20,...V2,^O20,^O^C^/20+...V2+20/
.IF	EQ	^/SIDES-1/
	BR	ENTRY
.IFF
.IF	EQ	^/SIDES-2./
	BMI	ENTRY
.IFF
	.ERROR;?SYSMAC-E-Invalid s i d e s value - sides;
.ENDC
.ENDC
.ENDM
.MACRO	.DRDEF	NAME,CODE,STAT,SIZE,CSR,VEC
.MCALL	.DRAST,.DRBEG,.DRBOT,.DREND,.DREST,.DRFIN,.DRINS
.MCALL	.DRPTR,.DRSET,.DRSPF,.DRVTB,.FORK,.QELDF
.IIF NDF RTE$M RTE$M=0
.IIF NE RTE$M RTE$M=1
.IIF NDF TIM$IT TIM$IT=0
.IIF NE TIM$IT TIM$IT=1
.IIF NDF MMG$T MMG$T=0
.IIF NE MMG$T MMG$T=1
.IIF NDF ERL$G ERL$G=0
.IIF NE ERL$G ERL$G=1
.IIF NE TIM$IT,	.MCALL	.TIMIO,.CTIMI
.QELDF
HDERR$=:1
EOF$=:0O20000
VARSZ$=:0O400
ABTIO$=:0O1000
SPFUN$=:0O2000
HNDLR$=:0O4000
SPECL$=:0O10000
WONLY$=:0O20000
RONLY$=:0O40000
FILST$=:0O100000
NAME'DSIZ=:SIZE
NAME'$COD=:CODE
NAME'STS=:^/CODE/!^/STAT/
.IIF NDF NAME'$VEC,NAME'$VEC=VEC
.GLOBL	NAME'$VEC
.ASECT
.=0O176
.IF DIF ^/CSR/,^/*NO*/
.IIF NDF NAME'$CSR,NAME'$CSR=CSR
.GLOBL	NAME'$CSR
	.WORD	NAME'$CSR
.ENDC
.ENDM
.MACRO .DREND	NAME,FORCE=0,PSECT,?L1,?L2
.IF B ^/PSECT/
.PSECT	NAME'DVR
.IFF
.PSECT	PSECT
.ENDC
.IIF NDF NAME'$END,NAME'$END::
.IF EQ	.-NAME'$END
.IF NE MMG$T!^/FORCE&2./
$RLPTR::.WORD	0
$MPPTR::.WORD	0
$GTBYT::.WORD	0
$PTBYT::.WORD	0
$PTWRD::.WORD	0
.ENDC
.IF NE ERL$G!^/FORCE&1/
$ELPTR::.WORD	0
.ENDC
.IF NE TIM$IT!^/FORCE&4./
$TIMIT::.WORD	0
.ENDC
$INPTR::.WORD	0
$FKPTR::.WORD	0
.GLOBL	NAME'STRT
NAME'END==.
.IFF
.PSECT	NAME'BOOT
.IIF LT	^/NAME'BOOT-.+0O664/,.ERROR;?SYSMAC-E-Primary boot too large;
.=NAME'BOOT+0O664
BIOERR:	JSR	R1,REPORT
	 .WORD	IOERR-NAME'BOOT
REPORT:	MOV	#BOOTF-NAME'BOOT,R0
	MOV	#L2-NAME'BOOT,R2
	CALL	@R2
	MOV	@R1,R0
	CALL	@R2
	MOV	#CRLFLF-NAME'BOOT,R0
	CALL	@R2
L1:	HALT
	BR	L1
L2:	TSTB	@#TPS
	BPL	L2
	MOVB	(R0)+,@#TPB
	BNE	L2
	RETURN
BOOTF:	.ASCIZ	^/CR/^/LF/"?BOOT-U-"
IOERR:	.ASCII	"I/O error"
CRLFLF:	.ASCIZ	^/CR/^/LF/^/LF/
	.EVEN
.IIF NDF ...V7,...V7=-1
.REPT	4.
	.WORD	...V7
.ENDR
NAME'BEND::
.ENDC
.ENDM
.MACRO	.DREST	CLASS=0,MOD=0,DATA=0,TYPE,REPLACE=0,MOD2=0
DVC.UK	=:0
DVC.NL	=:1
DVC.TT	=:0o2
DVC.TP	=:0o3
DVC.DK	=:0o4
DVC.MT	=:0o5
DVC.CT	=:0o6
DVC.LP	=:0o7
DVC.DE	=:0o10
DVC.DP	=:0o11
DVC.DL	=:0o12
DVC.NI	=:0o13
DVC.PS	=:0o14
DVC.VT	=:0o15

DVM.DX	=:1
DVM.DM	=:0o2
DVM.NF	=:0o200
DV2.V2	=:0o40000
.IIF NDF ...V18,...V18=0
.IIF NDF ...V19,...V19=0
...V18=...V18!^/MOD/
...V19=...V19!^/MOD2/
.ASECT
.=0
	.RAD50	"HAN"
.=0o20
	.BYTE	CLASS
.IRP	X,\...V18
	.BYTE	X
.ENDR
.=0o32
	.WORD	REPLACE
.=0o70
	.WORD	DATA
	.RAD50	"TYPE"
.IF B ^/TYPE/
.IIF DIF ^/DATA/,^/0/ .ERROR ;?SYSMAC-W-D A T A table specified, but no T Y P E;
.ENDC
.ENDM
.MACRO	.DRINS	NAME,CSRS
.ASECT
.=0o172
	.WORD	0
...V2=0
.IRPC X,^/NAME/
.IIF IDN ^/-/,^/X/ ...V2=1
.ENDR
.IF NE ...V2
DISCSR:	.WORD	-^/NAME'$CSR/
INSCSR:	.WORD	0
.IFF
DISCSR:	.WORD	NAME'$CSR
INSCSR:	.WORD	NAME'$CSR
.ENDC
...V2=2.
...V3=0o170
.IRP X,CSRS
.IRP Y,\...V2
.=...V3
	.WORD	0
DISCS'Y:	.WORD	X
.ENDR
...V2=...V2+1
...V3=...V3-0o2
.ENDR
.=0o202
INSSYS:
.=0o200
INSDAT:
.ENDM
.MACRO	.DRFIN	NAME
.GLOBL	NAME'CQE
	MOV	PC,R4
	ADD	#NAME'CQE-.,R4
	MOV	@#0O54,R5
	JMP	@0O270(R5)
.ENDM
.MACRO	.DRPTR	FETCH=0,RELEASE=0,LOAD=0,UNLOAD=0,FORMAT=0,SHOW=0
.IIF NDF ...V9,...V9=0
.IIF NDF ...V18,...V18=0
.IIF NDF ...V19,...V19=0
.ASECT
.=0
	.RAD50	"HAN"
.IF IDN ^/FETCH/,^/*NO*/
...V18=...V18!0o200
...V19=...V19!0o100000
	.WORD	0
.IFF
	.WORD	FETCH
.IF DIF ^/FETCH/,^/0/
...V9=...V9!1
.ENDC
.ENDC
	.WORD	RELEASE
	.WORD	LOAD
	.WORD	UNLOAD
	.WORD	FORMAT
	.WORD	SHOW
.IIF DIF ^/RELEASE/,^/0/	...V9=...V9!0o2
.IIF DIF ^/LOAD/,^/0/	...V9=...V9!0o4
.IIF DIF ^/UNLOAD/,^/0/	...V9=...V9!0o10
...V2=...V9/0o400&^o377
.=0o21
.IRP	X,\...V2
	.BYTE	X
.ENDR
.ENDM
.MACRO	.DRSET	OPTION,VAL,RTN,MODE
.ASECT
.IF LE	.-0O400
.=0O400
.IFF
.=.-2.
.ENDC
	VAL
...V2=.
	.RAD50	\OPTION\
.=...V2+4.
	.BYTE	^/RTN-0O400//2.
...V2=0
.IRP X,MODE
.IF IDN ^/X/,^/NUM/
...V2=...V2!0O100
.IFF
.IF IDN ^/X/,^/NO/
...V2=...V2!0O200
.IFF
.IF IDN ^/X/,^/OCT/
...V2=...V2!0O140
.IFF
.ERROR;?SYSMAC-E-Invalid parameter x;
.ENDC
.ENDC
.ENDC
.ENDR
	.BYTE	...V2
	.WORD	0
.ENDM
.MACRO	.DRSPF	ARG,ARG2
.MCALL	...CM8
...V10=0
.IRPC	X,^/ARG/
.IIF IDN ^/X/,^/+/ ...V10=1
.IIF IDN ^/X/,^/-/ ...V10=2
.MEXIT
.ENDR
.IF EQ ...V10-1
.=30
	.WORD	0'ARG'
.MEXIT
.ENDC
.IF EQ ...V10-2
...V2=0
...V3=0
.IRP X,ARG2
...V10=0
...V11=X&0o370
...CM8	X,...V3,...V2
.IF EQ ...V10
.IF EQ ...V2
...V2=...V11
.IFF
.IIF NE ...V2-...V11 .ERROR ;?SYSMAC-F-Too many different NN$ SPFUN codes;
.ENDC
.ENDC
...CM8	X,...V3,...V2
.ENDR
.IRP	X,\...V3
	.BYTE	X
.ENDR
.IRP	X,\...V2
	.BYTE	X
.ENDR
.MEXIT
.ENDC
.IF NDF ...V15
...V15=0
...V16=0
...V17=0
...V12=0
...V13=0
...V14=0
.ENDC
.IRP	X,ARG
...V10=0
...V11=X&0o370
...CM8	X,...V15,...V12
...CM8	X,...V16,...V13
...CM8	X,...V17,...V14
.IF EQ ...V10
.IF EQ ...V12
...V12=...V11
.IFF
.IF EQ ...V13
...V13=...V11
.IFF
.IF EQ ...V14
...V14=...V11
.IFF
.ERROR ;?SYSMAC-F-Too many different NN$ SPFUN codes;
.ENDC
.ENDC
.ENDC
.ENDC
...CM8	X,...V15,...V12
...CM8	X,...V16,...V13
...CM8	X,...V17,...V14
.ENDR
.ASECT
.=22
.IRP	X,...V15,...V12,...V16,...V13,...V17,...V14
.IRP	Y,\X
	.BYTE	Y
.ENDR
.ENDR
	.WORD	000000
.ENDM
.MACRO	...CM8	VAL,XXN,NNX
.IIF EQ ...V11&0o200 .ERROR VAL ;?SYSMAC-F-SPFUN values must be negative;
.IF EQ ...V11-NNX
...V10=1
.REPT VAL-...V11
...V10=...V10+...V10
.ENDR
XXN=XXN!...V10
.ENDC
.ENDM
.MACRO	.DRVTB	NAME,VEC,INT,PS=0,SLOTID
.IF NB	NAME
NAME'$VTB::
.IF NB	SLOTID
	.WORD	0O100000,SLOTID
.ENDC
.IFF
.=.-2.
.ENDC
.IIF NE	VEC&3.	.ERROR	VEC;?SYSMAC-E-Odd or invalid vector specified;
	.WORD	VEC&^C3.,INT-.,0O340!PS,^O100000
.ENDM
.MACRO	.DSTAT	RETSPC,DNAM
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM5	^/DNAM/
...CM0	^/RETSPC/,342
.ENDM
.MACRO	.ELAW	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,30,3,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.ELRG	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,30,1,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.ENTER	AREA,CHAN,DBLK,LEN,SEQNUM,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
...CM5	^/CHAN/
...CM0	^/DBLK/,^/40+AREA/
.IFF
...CM1	^/AREA/,2,^/CHAN/,^/CODE/,^/DBLK/
...CM2	^/LEN/,4,,C
...CM2	^/SEQNUM/,6,E,C
.ENDC
.ENDM
.MACRO	.EXIT
	EMT	0O350
.ENDM
.MACRO	.FETCH	ADDR,DNAM
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM5	^/DNAM/
...CM0	^/ADDR/,343
.ENDM
.MACRO	.FORK	FKBLK
	JSR	R5,@$FKPTR
	.WORD	FKBLK-.
.ENDM
.MACRO	.FPROT	AREA,CHAN,DBLK,PROT=#1,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM1	^/AREA/,35,^/CHAN/,^/CODE/,^/DBLK/
...CM2	^/PROT/,4,E,,B
.ENDM
.MACRO	.GMCX	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,30,6,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.GTIM	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,17,0,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.GTJB	AREA,ADDR,JOBBLK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,16,1,^/CODE/,^/ADDR/
.IF NB  ^/JOBBLK/
.IF IDN ^/JOBBLK/,^/ME/
...CM2	#-1,4,E
.IFF
...CM2	^/JOBBLK/,4,E
.ENDC
.IFF
...CM2	#-3.,4,E
.ENDC
.ENDM
.MACRO	.GTLIN	LINBUF,PROMPT,TYPE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM0	^/LINBUF/
.IF	NB	^/TYPE/
...CM0	#3.
.IFF
...CM0	#1
.ENDC
...CM0	^/PROMPT/
...CM0	,345
.ENDM
.MACRO	.GVAL	AREA,OFFSE,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,28,0,^/CODE/,^/OFFSE/,E
.ENDM
.MACRO	.HERR
	MOV	#0O2400,R0
	EMT	0O374
.ENDM
.MACRO	.HRESET
	EMT	0O357
.ENDM
.MACRO	.INTEN	PRIO,PIC
.IF B PIC
	JSR	R5,@0O54
.IFF
	MOV	@#0O54,-(SP)
	JSR	R5,@(SP)+
.ENDC
	.WORD	^C^/PRIO*32./&224.
.ENDM
.MACRO	.LOCK
	EMT	0O346
.ENDM
.MACRO	.LOOKUP	AREA,CHAN,DBLK,SEQNUM,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
...CM5	^/CHAN/,^/20+AREA/
.IFF
...CM1	^/AREA/,1,^/CHAN/,^/CODE/,^/DBLK/
...CM2	^/SEQNUM/,4,E,C
.ENDC
.ENDM
.MACRO	.MAP	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,30,4,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.MFPS	ADDR
	MOV	@#0O54,-(SP)
	ADD	#0O362,(SP)
	CALL	@(SP)+
.IF NB	^/ADDR/
	MOVB	(SP)+,ADDR
.ENDC
.ENDM
.MACRO	.MODULE	MODULE,VERSION,COMMENT,TITLE=YES,IDENT=YES,AUDIT=NO,GLOBAL,LIB=NO,MODNAME,RELEASE=V05
.MCALL	.AUDIT
.IIF	NDF,...V6,	...V6 = 0
...V6	= ...V6+1
.IF	NB	^/MODNAME/
.IIF	IDN	^/TITLE/ ^/YES/,	.TITLE	'MODNAME'	- 'COMMENT'
.IFF
.IIF	IDN	^/TITLE/ ^/YES/,	.TITLE	'MODULE'	- 'COMMENT'
.ENDC
.IIF	IDN	^/IDENT/ ^/YES/,	.IDENT	"'RELEASE'.'VERSION'"
.IIF	IDN	^/LIB/ ^/NO/,.AUDIT==:^R'RELEASE'
.IF	NB	^/GLOBAL/
'GLOBAL'==:'VERSION'.
.IIF	IDN	^/AUDIT/ ^/YES/,	.AUDIT	'GLOBAL'
.IFF
.'MODULE'==:'VERSION'.
.IIF	IDN	^/AUDIT/ ^/YES/,	.AUDIT	.'MODULE'
.ENDC
.MACRO	.NLCSI	TYPE=Z,PART=ALL
.IF	IDN	^/PART/ ^/ALL/
	.ASCI'TYPE'	"'MODULE' 'RELEASE'.'VERSION' "
.ENDC
.IF	IDN	^/PART/ ^/NAME/
	.ASCI'TYPE'	"'MODULE'"
.ENDC
.IF	IDN	^/PART/ ^/RLSVER/
	.ASCI'TYPE'	"'RELEASE'.'VERSION' "
.ENDC
.IF	IDN	^/PART/ ^/PREFIX/
	.ASCI'TYPE'	"?'MODULE'-"
.ENDC
.ENDM
.MACRO	.RMODULE
	.RAD50	"'MODULE'"
.ENDM
...V5=0O110
.ENDM
.MACRO	.MRKT	AREA,TIME,CRTN,ID,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,18,0,^/CODE/,^/TIME/
...CM2	^/CRTN/,4
...CM2	^/ID/,6,E
.ENDM
.MACRO	.MTATC	AREA,ADDR,UNIT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,5,^/CODE/,^/ADDR/
...CM2	^/UNIT/,4,E,,B
.ENDM
.MACRO	.MTDTC	AREA,UNIT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,6,^/CODE/
...CM2	^/UNIT/,4,E,,B
.ENDM
.MACRO	.MTGET	AREA,ADDR,UNIT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,1,^/CODE/,^/ADDR/
...CM2	^/UNIT/,4,E,,B
.ENDM
.MACRO	.MTIN	AREA,ADDR,UNIT,CHRCNT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,2,^/CODE/,^/ADDR/
...CM2	^/UNIT/,4,,,B
...CM2	^/CHRCNT/,5,E,,B
.ENDM
.MACRO	.MTOUT	AREA,ADDR,UNIT,CHRCNT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,3,^/CODE/,^/ADDR/
...CM2	^/UNIT/,4,,,B
...CM2	^/CHRCNT/,5,E,,B
.ENDM
.MACRO	.MTPRN	AREA,ADDR,UNIT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,7,^/CODE/,^/ADDR/
...CM2	^/UNIT/,4,E,,B
.ENDM
.MACRO	.MTPS	ADDR
.IF NB	^/ADDR/
	CLR	-(SP)
.ENDC
.IF NB	^/ADDR/
	MOVB	ADDR,(SP)
.ENDC
	MOV	@#0O54,-(SP)
	ADD	#0O360,(SP)
	CALL	@(SP)+
.ENDM
.MACRO	.MTRCT	AREA,UNIT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,4,^/CODE/
...CM2	^/UNIT/,4,E,,B
.ENDM
.MACRO	.MTSET	AREA,ADDR,UNIT,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,0,^/CODE/,^/ADDR/
...CM2	^/UNIT/,4,E,,B
.ENDM
.MACRO	.MTSTA	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,31,8,^/CODE/,^/ADDR/
...CM2	#0,4,E
.ENDM
.MACRO	.MWAIT
	MOV	#0O4400,R0
	EMT	0O374
.ENDM
.MACRO	.PEEK	AREA,ADDR,CODE
.IF NDF	...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,28,1,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.POKE	AREA,ADDR,VALUE,CODE
.IF NDF	...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,28,3,^/CODE/,^/ADDR/
...CM2	^/VALUE/,4,E
.ENDM
.MACRO	.PRINT	ADDR
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM5	^/ADDR/,351
.ENDM
.MACRO	.PROTE	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,25,0,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.PURGE	CHAN
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM3	^/CHAN/,3.
.ENDM
.MACRO	.PVAL	AREA,OFFSE,VALUE,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,28,2,^/CODE/,^/OFFSE/
...CM2	^/VALUE/,4,E
.ENDM
.MACRO	.QELDF
.IIF NDF MMG$T,MMG$T=1
.IIF NE MMG$T,MMG$T=1
Q.LINK=:0
Q.CSW=:2.
Q.BLKN=:4.
Q.FUNC=:6.
Q.JNUM=:7.
Q.UNIT=:7.
Q.BUFF=:0O10
Q.WCNT=:0O12
Q.COMP=:0O14
.IRP	X,LINK,CSW,BLKN,FUNC,JNUM,UNIT,BUFF,WCNT,COMP
Q$'X=:Q.'X-0o4
.ENDR
.IF EQ MMG$T
Q.ELGH=:0O16
.IFF
Q.PAR=:0O16
Q$PAR=:0O12
Q.ELGH=:0O24
.ENDC
.ENDM
.MACRO	.QSET	ADDR,LEN
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
	MOV	ADDR,-(SP)
...CM5	^/LEN/,353
.ENDM
.MACRO	.RCTRLO
	EMT	0O355
.ENDM
.MACRO	.RCVD	AREA,BUF,WCNT,CRTN=#1,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM4 ^/AREA/,^/BUF/,^/WCNT/,^/CRTN/,22,^/CODE/
.ENDM
.MACRO	.RCVDC	AREA,BUF,WCNT,CRTN,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM4 ^/AREA/,^/BUF/,^/WCNT/,^/CRTN/,22,^/CODE/
.ENDM
.MACRO	.RCVDW	AREA,BUF,WCNT,CRTN=#0,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM4 ^/AREA/,^/BUF/,^/WCNT/,^/CRTN/,22,^/CODE/
.ENDM
.MACRO	.RDBBK	RGSIZ,RGSTA,NAME
.MCALL	.RDBDF
.RDBDF
	.WORD
	.WORD	RGSIZ
	.WORD	RGSTA
	.WORD	0,0
.=.-4.
...V2=.
	.RAD50	\NAME\
.=...V2+4.
.ENDM
.MACRO	.RDBDF
R.GID=:0
R.GSIZ=:2.
R.GSTS=:4.
R.GNAM=:6.
R.GLGH=:10.
RS.CRR=:0O100000
RS.UNM=:0O40000
RS.NAL=:0O20000
RS.NEW=:0O10000
RS.GBL=:0O4000
RS.CGR=:0O2000
RS.AGE=:0O1000
RS.EGR=:0O400
RS.EXI=:0O200
.ENDM
.MACRO	.READ	AREA,CHAN,BUF,WCNT,BLK,CRTN=#1,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM7	^/AREA/,^/CHAN/,^/BUF/,^/WCNT/,^/BLK/,^/CRTN/,8,^/CODE/,200
.ENDM
.MACRO	.READC	AREA,CHAN,BUF,WCNT,CRTN,BLK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM7	^/AREA/,^/CHAN/,^/BUF/,^/WCNT/,^/BLK/,^/CRTN/,8,^/CODE/,200
.ENDM
.MACRO	.READW	AREA,CHAN,BUF,WCNT,BLK,CRTN=#0,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM7	^/AREA/,^/CHAN/,^/BUF/,^/WCNT/,^/BLK/,^/CRTN/,8,^/CODE/,200
.ENDM
.MACRO	.REGDEF
.ENDM
.MACRO	.RELEA	DNAM
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM5	^/DNAM/
...CM0	,343
.ENDM
.MACRO	.RENAME	AREA,CHAN,DBLK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
...CM5	^/CHAN/,^/100+AREA/
.IFF
...CM1	^/AREA/,4,^/CHAN/,^/CODE/,^/DBLK/,E
.ENDC
.ENDM
.MACRO	.REOPE	AREA,CHAN,CBLK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
...CM5	^/CHAN/,^/140+AREA/
.IFF
...CM1	^/AREA/,6,^/CHAN/,^/CODE/,^/CBLK/,E
.ENDC
.ENDM
.MACRO	.RSUM
	MOV	#0O1000,R0
	EMT	0O374
.ENDM
.MACRO	.SAVES	AREA,CHAN,CBLK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
...CM5	^/CHAN/,^/120+AREA/
.IFF
...CM1	^/AREA/,5,^/CHAN/,^/CODE/,^/CBLK/,E
.ENDC
.ENDM
.MACRO	.SCCA	AREA,ADDR,TYPE,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF	NB	^/TYPE/
...CM6	^/AREA/,29,1,^/CODE/,^/ADDR/,E
.IFF
...CM6	^/AREA/,29,0,^/CODE/,^/ADDR/,E
.ENDC
.ENDM
.MACRO	.SDAT	AREA,BUF,WCNT,CRTN=#1,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM4 ^/AREA/,^/BUF/,^/WCNT/,^/CRTN/,21,^/CODE/
.ENDM
.MACRO	.SDATC	AREA,BUF,WCNT,CRTN,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM4 ^/AREA/,^/BUF/,^/WCNT/,^/CRTN/,21,^/CODE/
.ENDM
.MACRO	.SDATW	AREA,BUF,WCNT,CRTN=#0,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM4 ^/AREA/,^/BUF/,^/WCNT/,^/CRTN/,21,^/CODE/
.ENDM
.MACRO	.SDTTM	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,32,0,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.SERR
	MOV	#0O2000,R0
	EMT	0O374
.ENDM
.MACRO	.SETTOP	ADDR
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM5	^/ADDR/,354
.ENDM
.MACRO	.SFDAT	AREA,CHAN,DBLK,DATE=#0,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM1	^/AREA/,34,^/CHAN/,^/CODE/,^/DBLK/
...CM2	^/DATE/,4,E
.ENDM
.MACRO	.SFPA	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,24,0,^/CODE/,^/ADDR/,E
.ENDM
; remove due to conflict, as it was not imported by .mcall
;.MACRO	SOB	R,DST
;	DEC	R
;	BNE	DST
;.ENDM
.MACRO	.SPCPS	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,33,0,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.SPFUN	AREA,CHAN,FUNC,BUF,WCNT,BLK,CRTN=#0,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM1	^/AREA/,26,^/CHAN/,^/CODE/,^/BLK/
...CM2	^/BUF/,4
...CM2	^/WCNT/,6
.IF NB FUNC
.NTYPE ...V2,FUNC
.IF NE ...V2-0O27
.IIF DIF ^/CODE/,NOSET,...CM2	#0O377,8,,,B
...CM2	^/FUNC/,9,,,B
.IFF
...CM2	^/FUNC'*0O400+0O377/,8
.ENDC
.ENDC
...CM2	^/CRTN/,10,E,C
.ENDM
.MACRO	.SPND
	MOV	#0O400,R0
	EMT	0O374
.ENDM
.MACRO	.SRESE
	EMT	0O352
.ENDM
.MACRO	.SYNCH	AREA,PIC
.IF B PIC
.IF NB ^/AREA/
	MOV	AREA,R4
.ENDC
.IFF
.IF NB AREA
	MOV	PC,R4
	ADD	#AREA-.,R4
.ENDC
.ENDC
	MOV	@#0O54,R5
	JSR	R5,@0O324(R5)
.ENDM
.MACRO	.TIMIO	TBK,HI,LO
	JSR	R5,@$TIMIT
	.WORD	TBK-.
	.WORD	0
	.WORD	HI
	.WORD	LO
.ENDM
.MACRO	.TLOCK
	MOV	#0O3400,R0
	EMT	0O374
.ENDM
.MACRO	.TRPSE	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,3,0,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.TTINR
	EMT	0O340
.ENDM
.MACRO	.TTOUTR
	EMT	0O341
.ENDM
.MACRO	.TTYIN	CHAR
	EMT	0O340
	BCS	.-2.
.IF NB ^/CHAR/
.IF DIF ^/CHAR/,R0
	MOVB	R0,CHAR
.ENDC
.ENDC
.ENDM
.MACRO	.TTYOUT	CHAR
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM5	^/CHAR/,341,B
	BCS	.-2.
.ENDM
.MACRO	.TWAIT	AREA,TIME,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,20,0,^/CODE/,^/TIME/,E
.ENDM
.MACRO	.UNLOCK
	EMT	0O347
.ENDM
.MACRO	.UNMAP	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,30,5,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.UNPRO	AREA,ADDR,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM6	^/AREA/,25,1,^/CODE/,^/ADDR/,E
.ENDM
.MACRO	.WAIT	CHAN
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
.IF EQ ...V1-1
	EMT	^/240+CHAN/
.IFF
.IF B ^/CHAN/
	CLR	R0
.IFF
.NTYPE ...V2,CHAN
.IF EQ ...V2-0O27
.IF IDN ^/CHAN/,#0
	CLR	R0
.IFF
.IIF IDN ^/CHAN/,0,.ERROR;?SYSMAC-W-Invalid argument, use #0, not 0;
	MOV	CHAN,R0
.ENDC
.IFF
	CLR	R0
	BISB	CHAN,R0
.ENDC
.ENDC
	EMT	0O374
.ENDC
.ENDM
.MACRO	.WDBBK	WNAPR,WNSIZ,WNRID,WNOFF,WNLEN,WNSTS
.MCALL	.WDBDF
.WDBDF
	.BYTE
	.BYTE	WNAPR
	.WORD
	.WORD	WNSIZ
	.WORD	WNRID
	.WORD	WNOFF
	.WORD	WNLEN
	.WORD	WNSTS
.ENDM
.MACRO	.WDBDF
W.NID=:0
W.NAPR=:1
W.NBAS=:2.
W.NSIZ=:4.
W.NRID=:6.
W.NOFF=:0O10
W.NLEN=:0O12
W.NSTS=:0O14
W.NLGH=:0O16
WS.CRW=:0O100000
WS.UNM=:0O40000
WS.ELW=:0O20000
WS.MAP=:0O400
.ENDM
.MACRO	.WRITC	AREA,CHAN,BUF,WCNT,CRTN,BLK,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM7	^/AREA/,^/CHAN/,^/BUF/,^/WCNT/,^/BLK/,^/CRTN/,9,^/CODE/,220
.ENDM
.MACRO	.WRITE	AREA,CHAN,BUF,WCNT,BLK,CRTN=#1,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM7	^/AREA/,^/CHAN/,^/BUF/,^/WCNT/,^/BLK/,^/CRTN/,9,^/CODE/,220
.ENDM
.MACRO	.WRITW	AREA,CHAN,BUF,WCNT,BLK,CRTN=#0,CODE
.IF NDF ...V1
.MCALL	.MACS
.MACS
.ENDC
...CM7	^/AREA/,^/CHAN/,^/BUF/,^/WCNT/,^/BLK/,^/CRTN/,9,^/CODE/,220
.ENDM
